*
* Print primes numbers from 1 upto 100 
* (using short form of commands)
* Language: M-20 autocode 
* encoding: russian, cp-1251
*
* Copyright (c) 2015 Stefankov
*

	.ИМЯ  primes1.a20
	.НАЗВАНИЕ  Программа печати простых числел для ЭЦВМ M-20
	.АВТОР  Стефанков Дмитрий
*
*
NUM100 	.АБС	144
ONE      .АБС	=1

	.СТАРТ  20
N1:  ONE 		; N
N2:  ONE
N3:  NUM100
*
*
	.АДРЕС  40
WORK:                   ; рабочие ячейки
*
*
	.АДРЕС  100
@НАЧАЛО:
        п   N1, 0, WORK
        ра  ,,
;
;подготовить массив
;
@FILL:                                 ; 
        п   WORK,0, (PRIMES)
        с   WORK, N2, WORK        
        пм  NUM100-1, @FILL, (1)
;
; печать массива [1..100]
;
        ра  ,,
@PRINT1:
        п   (PRIMES), 0, WORK
        ма  2100, 0, WORK
        мб  WORK
        пм  NUM100-1, @PRINT1, (1)
;
; test=sqrt(N)
;
        к   WORK, 0, WORK+1
        ма  2100, 0, WORK+1
        мб  WORK+1
;
; удаляем числа, которые делятся на 2
;
        ра  0, 3, 
@DIV2:
        п   0, 0, (PRIMES)
        пм  NUM100-2, @DIV2, (2)
;
; удаляем числа, которые делятся на 3
;
        ра  0, 5,
@DIV3:
        п   0, 0, (PRIMES)
        пм  NUM100-2, @DIV3, (3)
;
; удаляем числа, которые делятся на 5
;
        ра  0, 011,
@DIV5:
        п   0, 0, (PRIMES)
        пм  NUM100-2, @DIV5, (5)
;
; удаляем числа, которые делятся на 7
;
        ра  0, 015,
@DIV7:
        п   0, 0, (PRIMES)
        пм  NUM100-2, @DIV7, (7)
;
; печать простых числел из массива [1..100]
;
        ра  0, 1,
@PRINT3:
        п   (PRIMES), 0, WORK
        н   WORK, 0,
        пе  0, @NEXT,
        ма  2100, 0, WORK
        мб  WORK
@NEXT:
        пм  NUM100-3, @PRINT3, (1)
;
; завершение
;
	стоп                              ;  ОСТАНОВ ЭЦВМ
        стоп  (7777),(7777),(7777)        ; останов после выдачи
*
*
	.АДРЕС  1000
PRIMES:  .ПАМ  144

	.ФИНИШ 100

* конец программы

