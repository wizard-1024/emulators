*
*
* Загрузка ИС-2 и расчет фунции Лапласа
* (использует готовые образы МБ из is2_build)
* Источник: сборник научных трудов N50, 1964, МО СССР
* Language: M-20 autocode 
* encoding: russian, utf8
*
* Copyright (c) 2015 Stefankov
*

	.ИМЯ  strela_1964.a20
	.НАЗВАНИЕ  Расчет фунции Лапласа для ЭЦВМ M-20
	.АВТОР  Стефанков Дмитрий
*
*
ИС2_НАЧАЛО 		.АБС	7500
ИС2_КОНЕЦ	  	.АБС	7767
ИС2_СОХР                .АБС    7610
ИС2_МБ3_АДР_ЗГР 	.АБС	7000
МБ3_ЧТЕНИЕ      	.АБС	0013
ДЕСЯТ_ПЕЧАТЬ            .АБС    2100
СЧЕТЧИК                 .АБС    017
СЧЕТЧИК1                .АБС    01
*
*
	.СТАРТ  100
; Данные
АРГ:
0 72 5075 3412 1727
0 74 6314 6314 6315
0 75 6314 6314 6315
0 77 4631 4631 4632
1 00 7146 3146 3146
1 01 4314 6314 6315
1 01 6000 0000 0000
1 02 4000 0000 0000
1 02 6000 0000 0000
1 02 7000 0000 0000
1 03 4000 0000 0000
1 03 4400 0000 0000
1 03 5000 0000 0000
1 03 6000 0000 0000
1 03 7000 0000 0000
;      =1E-2 ;0.01
;      =5E-2 ;0.05
;      =1E-1 ;0.10
;      =3E-1 ;0.30
;      =9E-1 ;0.90
;      =1.10
;      =1.50
;      =2.00
;      =3.00
;      =3.50
;      =4.00
;      =4.50
;      =5.00
;      =6.00
;      =7.00
*
*
	.АДРЕС  200
ТАБЛ:
0 71 5404 5266 0153     
0 73 6705 6543 4206
0 74 6703 6756 3602
0 76 5103 1312 2572
0 77 7231 0154 6243
1 00 4253 4245 6346
1 00 5403 3145 0556
1 00 6451 4730 4074
1 00 7517 6244 1257
1 00 7665 2237 4510
1 00 7743 3217 0204
1 00 7766 1266 5350
1 00 7774 7454 7626
1 00 7777 6227 4533
1 00 7777 7715 5262
;      =5.38E-3    ;0,0053800
;      =2.69E-2    ;0.0269000
;      =5.377E-2   ;0.0537700
;      =1.6035E-1  ;0.1603500
;      =4.5618E-1  ;0.4561800
;      =5.4188E-1  ;0.5418800
;      =6.8833E-1  ;0.6883300
;      =8.2266E-1  ;0.8226600
;      =9.5698E-1  ;0.9569800
;      =9.8176E-1  ;0.9817600
;      =9.9302E-1  ;0.9930200
;      =9.976E-1   ;0.9976000
;      =9.99255E-1 ;0.9992550
;      =9.99948E-1 ;0.9999480
;      =9.99997E-1 ;0.9999970
*
*
       .АДРЕС  300
F1_ARG_X: 1 00 4234 2332 4400   ;=0.53816
F1_C1:   =1E+0
F1_D:    2 00 0000 0000 0000
F1_T1:   =0
F1_T2:   =0
F1_T3:   =0
F1_T4:   =0
F1_T5:   =0
F1_T6:   =0
F1_T7:   =0
F1_T8:   =0
F1_RES:  =0
*
*
       .АДРЕС  400
F3_D:    2 00 0000 0000 0000
F3_A1:   3 06 4353 4532 2102   ; =-35.6810084
F3_A2:   1 04 5357 6476 1502   ; =10.9368269
F3_A3:   3 01 7035 6440 6576   ; =-1.7645608
F3_A4:   0 73 6371 6137 3221   ; =0.025343119
F3_A5:   0 72 7106 1034 2303   ; =0.013939407
F3_B0:   3 07 4111 5116 2535   ; =-66.3013789
F3_B1:   1 05 5051 3315 7446   ; =20.323635
F3_B2:   3 04 4117 4140 3225   ; =-8.31063882
F3_B3:   1 01 6310 1067 3724   ; =1.5977239
F3_B4:   2 77 4273 7345 4724   ; =-0.272940803
F3_B5:   0 73 6242 4224 7300   ; =0.024677554
F3_MOD_X: =0
F3_RES:  =0
F3_C1:   =1E+0
F3_X1:   =0
F3_X2:   =0
F3_X3:   =0
F3_X4:   =0
F3_X5:   =0
F3_A1X1: =0
F3_A2X2: =0
F3_A3X3: =0
F3_A4X4: =0
F3_A5X5: =0
F3_B1X1: =0
F3_B2X2: =0
F3_B3X3: =0
F3_B4X4: =0
F3_B5X5: =0
F3_SUM_A: =0
F3_SUM_B: =0
F3_T1:   =0
F3_T2:   =0
F3_T3:   =0
*
*
       .АДРЕС  500
F2_MOD: 2 00 0000 0000 0000
F2_C1:   =1E+0
F2_A:  1 00 4137 3375 1040  ; =0.5233
F2_B:  2 75 5605 0753 4122  ; =-0.09
F2_C:  1 00 4671 4307 7050  ; =0.6078
F2_D:  2 73 4536 4741 5411  ; =-0.0183
F2_E:  2 75 5634 0353 7340  ; =-0.0907
F2_F:  1 00 4544 2641 6254  ; =0.587
F2_Q:  0 72 5503 4530 0406  ; =0.011
F2_H:  0 74 5442 6416 2540  ; =0.0435
F2_S:  1 00 6470 7363 2327  ; =0.8264
F2_T:  0 73 4605 7406 7551  ; =0.0186
F2_W:  1 00 7262 4536 4742  ; =0.9186
F2_MOD_X: =0
F2_RES:   =0
F2_T1:   =0
F2_T2:   =0
F2_T3:   =0
F2_X2:   =0
F2_SUM:  =0
*
*
       .АДРЕС  600
РАБ_ЯЧ:
*
*
*
	.АДРЕС  1000
@НАЧАЛО:
; Загружаем ИС-2
	ввод_пред_050    МБ3_ЧТЕНИЕ, ИС2_МБ3_АДР_ЗГР, ИС2_КОНЕЦ
	ввод_исп_070     ИС2_НАЧАЛО

;
; печать аргумента х
;
	изм_ра_по_адр    ,,    		; очистка РА
@ЦИКЛ_1:
        пересылка	 (АРГ), 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  СЧЕТЧИК-1, @ЦИКЛ_1, (1)


; разделяющая печать
        пересылка	 0, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ


;
; печать результата из таблицы
;
	изм_ра_по_адр    ,,    		; очистка РА
@ЦИКЛ_2:
        пересылка	 (ТАБЛ), 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  СЧЕТЧИК-1, @ЦИКЛ_2, (1)


; разделяющая печать
        пересылка	 0, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ


;
; расчет по формуле (1) => F1
;
	изм_ра_по_адр    ,,    		; очистка РА
@ЦИКЛ_3:
        умнож_он         F1_ARG_X, (АРГ), F1_T1   ; y = pow((0.53816*x),2);
        умнож_он         F1_T1, F1_T1, F1_T2
        вычит_мод        0, F1_T2, F1_T3          ; -y
	безусл_пер_упр_с_возв    *+1, ИС2_НАЧАЛО+1, ИС2_СОХР    ; z=exp(-y) вызов СП 3, y=exp(x)
	лог_слож F1_T3 3, F1_T4	                  ; адрес x, номер СП, адрес y
        вычит_он          F1_C1, F1_T4, F1_T5     ; 1-z
        извл_кв_корня_о   F1_T5, 0, F1_T6         ; w=sqrt(1-z)
        лог_умнож         F1_D, (АРГ), F1_T7      
        вычит_мод         F1_T6, 0, F1_T8
        сравн             F1_T7, F1_T8, F1_RES    ; res = sign(x)*w
;;;     умнож_он          F1_T7, F1_T8, F1_RES    ; res = sign(x)*w
; печать результата по формуле (1) = F1
        пересылка	 F1_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  СЧЕТЧИК-1, @ЦИКЛ_3, (1)
        ;;;пер_если_ра_мнш_a1  СЧЕТЧИК1-1, @ЦИКЛ_3, (1)


; разделяющая печать
        пересылка	 0, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ

;
; расчет по формуле (3) => F3
;
	изм_ра_по_адр    ,,    		; очистка РА
@ЦИКЛ_4:
        ;;пересылка	 (АРГ), 0, F3_MOD_X
        ;;;пересылка	 F3_MOD_X, 0, F3_RES
        вычит_мод        (АРГ), 0, F3_MOD_X        ; =|X|
        пересылка	 F3_MOD_X, 0, F3_X1        ; =|X|**1
        умнож_он	 F3_X1, F3_X1, F3_X2       ; =|X|**2
        умнож_он	 F3_X1, F3_X2, F3_X3       ; =|X|**3
        умнож_он	 F3_X2, F3_X2, F3_X4       ; =|X|**4
        умнож_он	 F3_X1, F3_X4, F3_X5       ; =|X|**5
        умнож_он	 F3_X1, F3_A1, F3_A1X1     ; =a1*|x|**1
        умнож_он	 F3_X2, F3_A2, F3_A2X2     ; =a2*|x|**2
        умнож_он	 F3_X3, F3_A3, F3_A3X3     ; =a3*|x|**3
        умнож_он	 F3_X4, F3_A4, F3_A4X4     ; =a4*|x|**4
        умнож_он	 F3_X5, F3_A5, F3_A5X5     ; =a5*|x|**5
        умнож_он	 F3_X1, F3_B1, F3_B1X1     ; =b1*|x|**1
        умнож_он	 F3_X2, F3_B2, F3_B2X2     ; =b2*|x|**2
        умнож_он	 F3_X3, F3_B3, F3_B3X3     ; =b3*|x|**3
        умнож_он	 F3_X4, F3_B4, F3_B4X4     ; =b4*|x|**4
        умнож_он	 F3_X5, F3_B5, F3_B5X5     ; =b5*|x|**5
        пересылка	 0, 0, F3_SUM_A
        слож_он          F3_A1X1, F3_SUM_A, F3_SUM_A
        слож_он          F3_A2X2, F3_SUM_A, F3_SUM_A
        слож_он          F3_A3X3, F3_SUM_A, F3_SUM_A
        слож_он          F3_A4X4, F3_SUM_A, F3_SUM_A
        слож_он          F3_A5X5, F3_SUM_A, F3_SUM_A
        пересылка	 0, 0, F3_SUM_B
        слож_он          F3_B0,   F3_SUM_B, F3_SUM_B
        слож_он          F3_B1X1, F3_SUM_B, F3_SUM_B
        слож_он          F3_B2X2, F3_SUM_B, F3_SUM_B
        слож_он          F3_B3X3, F3_SUM_B, F3_SUM_B
        слож_он          F3_B4X4, F3_SUM_B, F3_SUM_B
        слож_он          F3_B5X5, F3_SUM_B, F3_SUM_B
        делен_о          F3_SUM_A, F3_SUM_B, F3_T1
        лог_умнож        F3_D, (АРГ), F3_T2      
        вычит_мод        F3_T1, 0, F3_T3
        сравн            F3_T2, F3_T3, F3_RES    ; res = sign(x)*w
; печать результата по формуле (3) = F3
        пересылка	 F3_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  СЧЕТЧИК-2, @ЦИКЛ_4, (1)
; если X >6.08
        пересылка        F3_C1, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ

; разделяющая печать
        пересылка	 0, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ


;
; расчет по формуле (2) => F2
;
	изм_ра_по_адр    ,,    		; очистка РА

; 0=<X<0.6
@ЦИКЛ_5:
        вычит_мод        (АРГ), 0, F2_MOD_X    ; =|X|
        умнож_он         F2_A, F2_MOD_X, F2_T1 ; w=a*|x|    
        лог_умнож        F2_MOD, (АРГ), F2_T2  
        вычит_мод        F2_T1, 0, F2_T3
        сравн            F2_T2, F2_T3, F2_RES    ; res = sign(x)*w
        пересылка	 F2_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  4-1, @ЦИКЛ_5, (1)

; 0.6=<X<1.2
@ЦИКЛ_6:
        вычит_мод        (АРГ), 0, F2_MOD_X    ; =|X|
        умнож_он         F2_MOD_X, F2_MOD_X, F2_X2  ; =x**2
        пересылка	 F2_D, 0, F2_SUM
        умнож_он         F2_B, F2_X2, F2_T1       ; =b*x**2
        умнож_он         F2_C, F2_MOD_X, F2_T2    ; =c*x
        слож_он          F2_T1, F2_SUM, F2_SUM
        слож_он          F2_T2, F2_SUM, F2_SUM
        лог_умнож        F2_MOD, (АРГ), F2_T2  
        вычит_мод        F2_SUM, 0, F2_T3
        сравн            F2_T2, F2_T3, F2_RES    ; res = sign(x)*w
        пересылка	 F2_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  6-1, @ЦИКЛ_6, (1)

; 1.2=<X<3
@ЦИКЛ_7:
        вычит_мод        (АРГ), 0, F2_MOD_X    ; =|X|
        умнож_он         F2_MOD_X, F2_MOD_X, F2_X2  ; =x**2
        пересылка	 F2_Q, 0, F2_SUM
        умнож_он         F2_E, F2_X2, F2_T1       ; =b*x**2
        умнож_он         F2_F, F2_MOD_X, F2_T2    ; =c*x
        слож_он          F2_T1, F2_SUM, F2_SUM
        слож_он          F2_T2, F2_SUM, F2_SUM
        лог_умнож        F2_MOD, (АРГ), F2_T2  
        вычит_мод        F2_SUM, 0, F2_T3
        сравн            F2_T2, F2_T3, F2_RES    ; res = sign(x)*w
        пересылка	 F2_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  10-1, @ЦИКЛ_7, (1)


; 3=<X<3.7
@ЦИКЛ_8:
        вычит_мод        (АРГ), 0, F2_MOD_X    ; =|X|
        умнож_он         F2_H, F2_MOD_X, F2_T1 ; w=h*|x|    
        слож_он          F2_T1, F2_S, F2_SUM   ; w=h*|x|+s
        лог_умнож        F2_MOD, (АРГ), F2_T2  
        вычит_мод        F2_SUM, 0, F2_T3
        сравн            F2_T2, F2_T3, F2_RES    ; res = sign(x)*w
        пересылка	 F2_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  12-1, @ЦИКЛ_8, (1)

; 3.7=<X<5
@ЦИКЛ_9:
        вычит_мод        (АРГ), 0, F2_MOD_X    ; =|X|
        умнож_он         F2_T, F2_MOD_X, F2_T1 ; w=t*|x|    
        слож_он          F2_T1, F2_W, F2_SUM   ; z=t*|x|+w
        лог_умнож        F2_MOD, (АРГ), F2_T2  
        вычит_мод        F2_SUM, 0, F2_T3
        сравн            F2_T2, F2_T3, F2_RES    ; res = sign(x)*z
        пересылка	 F2_RES, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
; продолжить цикл?
	пер_если_ра_мнш_a1  14-1, @ЦИКЛ_9, (1)

; если X >=5
        пересылка        F2_C1, 0, РАБ_ЯЧ
        ввод_пред_050    ДЕСЯТ_ПЕЧАТЬ, 0, РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ
        ввод_исп_070     РАБ_ЯЧ

;
; завершение
;
	останов_077                              ;  ОСТАНОВ ЭЦВМ
        останов_077  (7777),(7777),(7777)        ; останов после выдачи
*
*
	.ФИНИШ 1000

* конец программы

