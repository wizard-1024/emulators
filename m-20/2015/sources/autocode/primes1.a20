*
* Print primes numbers from 1 upto 100 
* Language: M-20 autocode 
* encoding: russian, cp-1251
*
* Copyright (c) 2015 Stefankov
*

	.ИМЯ  primes1.a20
	.НАЗВАНИЕ  Программа печати простых числел для ЭЦВМ M-20
	.АВТОР  Стефанков Дмитрий
*
*
NUM100 	.АБС	144
ONE      .АБС	=1

	.СТАРТ  20
N1:  ONE 		; N
N2:  ONE
N3:  NUM100
*
*
	.АДРЕС  40
WORK:                   ; рабочие ячейки
*
*
	.АДРЕС  100
@НАЧАЛО:
        пересылка   N1, 0, WORK
        изм_ра_по_адр  ,,
;
;подготовить массив
;
@FILL:                                 ; 
        пересылка WORK,0, (PRIMES)
        слож_он WORK, N2, WORK        
        пер_если_ра_мнш_a1 NUM100-1, @FILL, (1)
;
; печать массива [1..100]
;
        изм_ра_по_адр  ,,
@PRINT1:
        пересылка  (PRIMES), 0, WORK
        ввод_пред_050  2100, 0, WORK
        ввод_исп_070  WORK
        пер_если_ра_мнш_a1 NUM100-1, @PRINT1, (1)
;
; test=sqrt(N)
;
        извл_кв_корня_о  WORK, 0, WORK+1
        ввод_пред_050  2100, 0, WORK+1
        ввод_исп_070  WORK+1
;
; удаляем числа, которые делятся на 2
;
        изм_ра_по_адр  0, 3, 
@DIV2:
        пересылка  0, 0, (PRIMES)
        пер_если_ра_мнш_a1 NUM100-2, @DIV2, (2)
;
; удаляем числа, которые делятся на 3
;
        изм_ра_по_адр  0, 5,
@DIV3:
        пересылка  0, 0, (PRIMES)
        пер_если_ра_мнш_a1 NUM100-2, @DIV3, (3)
;
; удаляем числа, которые делятся на 5
;
        изм_ра_по_адр  0, 011,
@DIV5:
        пересылка  0, 0, (PRIMES)
        пер_если_ра_мнш_a1 NUM100-2, @DIV5, (5)
;
; удаляем числа, которые делятся на 7
;
        изм_ра_по_адр  0, 015,
@DIV7:
        пересылка  0, 0, (PRIMES)
        пер_если_ра_мнш_a1 NUM100-2, @DIV7, (7)
;
; печать простых числел из массива [1..100]
;
        изм_ра_по_адр  0, 1,
@PRINT3:
        пересылка  (PRIMES), 0, WORK
        сравн  WORK, 0,
        усл_пер_упр_по_w1  0, @NEXT,
        ввод_пред_050  2100, 0, WORK
        ввод_исп_070  WORK
@NEXT:
        пер_если_ра_мнш_a1   NUM100-3, @PRINT3, (1)
;
; завершение
;
	останов_077                              ;  ОСТАНОВ ЭЦВМ
        останов_077  (7777),(7777),(7777)        ; останов после выдачи
*
*
	.АДРЕС  1000
PRIMES:  .ПАМ  144

	.ФИНИШ 100

* конец программы

